# Grammar for Fuga

.include ('include/ast.h')
.include ('include/internal/static_parser.h')
.fdecl ()

```c
// Placeholder node
static inline void *node(parser_t *p) {
    return parser_alloc(p, sizeof(char));
}

// Do nothing operation
static ast_stmt_t *ast_nop(parser_t *p) {
    return node(p);
}
```

single_input[ast_list_t *]:
    | NEWLINE { ast_list_of(p, ast_nop(p)) }
    | simple_statements { ast_list_of(p, %a) }
    | compound_statement NEWLINE { ast_list_of(p, %a) }
file_input[ast_list_t *]:
    | statement_list { a }
eval_input[ast_list_t *]:
    | expressions [NEWLINE] { a }

# Statements
statement_list[ast_list_t *]:
    | line_statement+ { a }
line_statement[ast_list_t *]:
    | simple_statements NEWLINE { a }
    | compound_statement NEWLINE { ast_list_of(p, %a) }

simple_statements[ast_list_t *]:
    | ';'.simple_statement+ [';'] { a }

```c
static ast_stmt_t *ast_break(parser_t *p) {
    return node(p);
}

static ast_stmt_t *ast_continue(parser_t *p) {
    return node(p);
}

static ast_stmt_t *ast_del(parser_t *p, ast_list_t *li) {
    return node(p);
}

static ast_stmt_t *ast_return(parser_t *p, ast_list_t *li) {
    return node(p);
}

static ast_named_t *ast_named(parser_t *p, token_t *name, ast_expr_t *expr) {
    return node(p);
}
```

# Small Statements
simple_statement[ast_stmt_t *]:
    | assignment { a }
    | starred_expressions { node(p) }
    | 'pass' { ast_nop(p) }
    | 'break' { ast_break(p) }
    | 'continue' { ast_continue(p) }
    | 'return' [starred_expressions] >> { ast_return(p, %a) }
    | 'raise' expression ['from' expression >> [ast_expr_t *]
        { a }] >> { ast_nop(p) }
    | 'del' targets >> { ast_del(p, %a) }


# Targets

target (memo):
    | t_primary '.' NAME !t_lookahead >> { node(p) }
    | t_primary subscript !t_lookahead >> { node(p) }
    | NAME
t_primary[ast_primary_t *] (left_recursive):
    | t_primary '.' NAME &t_lookahead >> { node(p) }
    | t_primary invocation &t_lookahead >> { node(p) }
    | t_primary subscript &t_lookahead >> { node(p) }
    | atom &t_lookahead >> { a }
t_lookahead[void *] (inline):
    | '.'
    | '('
    | '['

targets[ast_list_t *]:
    | ','.target+ [','] { a }


# Assignments

assignment:
    | target ':' expression ['=' expressions >> { a }] >> { node(p) }
    | (targets '=' >> { a })+ starred_expressions >> { node(p) }
    | target augmented_assign expressions >> { node(p) }

augmented_assign[int] (inline):
    | '+='  { BINOP_PLS }
    | '-='  { BINOP_MIN }
    | '*='  { BINOP_MUL }
    | '/='  { BINOP_DIV }
    | '%='  { BINOP_MOD }
    | '@='  { BINOP_MML }
    | '|='  { BINOP_IOR }
    | '&='  { BINOP_AND }
    | '^='  { BINOP_XOR }
    | '//=' { BINOP_FDV }
    | '**=' { BINOP_POW }
    | '<<=' { BINOP_SHL }
    | '>>=' { BINOP_SHR }


# Compound statements

compound_statement:
    | if_statement
    | while_statement
    | for_statement
    | try_statement
    | with_statement

if_statement:
    | 'if' named_expression suite elif_statement* [else_suite]
elif_statement:
    | 'elif' named_expression suite
while_statement:
    | 'while' named_expression suite [else_suite]
for_statement:
    | 'for' targets 'in' expressions suite [else_suite]
try_statement:
    | 'try' suite (except_suite | finally_suite)
with_statement:
    | 'with' ','.expression_as_name+ suite
expression_as_name:
    | expression ['as' NAME >> [token_t *]{ a }]

# Suites

block_suite:
    | '{' NEWLINE statement_list '}' { b }
    | '{' [simple_statements] '}' { a }
suite:
    | ':' simple_statements { a }
    | block_suite { a }
else_suite (inline):
    | 'else' suite { a }
finally_suite (inline):
    | 'finally' suite { a }
except_clause:
    | 'except' [expression_as_name] suite
except_suite:
    | except_clause+ [else_suite] [finally_suite]

# Invocation and Subscripts

invocation:
    | '(' [','.call_argument+  [','] { a } ] ')' { a }
call_argument:
    | NAME ':=' expression >> { ast_named(p, %a, %b) }
    | NAME '=' expression >> { ast_named(p, %a, %b) }
    | '**' expression >> { a }
    | '*' expression >> { a }
    | expression { a }

subscript[ast_list_t *]:
    | '[' ','.slice+ [','] ']' { a }
slice:
    | [expression] slice_expression [slice_expression]
    | expression
slice_expression:
    | ':' [expression] { a }


# Expressions

expressions[ast_list_t *]:
    | ','.expression+ [','] { a }
starred_expression[ast_expr_t *] (inline):
    | '*' bitwise_or { a }
maybe_starred[ast_expr_t *]:
    | starred_expression
    | expression
starred_expressions[ast_list_t *]:
    | ','.maybe_starred+ [','] { a }

```c
static ast_expr_t *ast_conditional(parser_t *p,
        ast_expr_t *cond, ast_expr_t *a, ast_expr_t *b) {
    return node(p);
}

static ast_named_t *ast_unnamed(parser_t *p, ast_expr_t *expr) {
    return ast_named(p, NULL, expr);
}

static ast_expr_t *ast_comp_term(parser_t *p,
        int comp_op, ast_expr_t *operand) {
    return node(p);
}

static ast_expr_t *ast_comparison(parser_t *p,
        ast_expr_t *first, ast_list_t *terms) {
    return node(p);
}
```

named_expression[ast_named_t *]:
    | NAME ':=' expression >> { ast_named(p, %a, %b) }
    | expression { ast_unnamed(p, %a) }

conditional[ast_expr_t *] (inline):
    | 'if' disjunction '?' disjunction ':' expression
        { ast_conditional(p, %a, %b, %c) }

expression[ast_expr_t *]:
    | conditional { a }
    | disjunction { a }

```c
// Construct a binary operation
static ast_expr_t *ast_binary(parser_t *p, ast_expr_t *left,
        ast_expr_t *right, enum expr_opcode binop_code) {
    ast_expr_t *r = parser_alloc(p, sizeof(ast_expr_t));
    r->opcode = binop_code;
    r->left = left;
    r->right.expr = right;
    return r;
}

// Construct a unary operation
static ast_expr_t *ast_unary(parser_t *p, ast_expr_t *expr,
        enum expr_opcode unary_opcode) {
    return ast_binary(p, expr, NULL, unary_opcode);
}
```

disjunction[ast_expr_t *] (left_recursive):
    | disjunction 'or' conjunction >>
        { ast_binary(p, %a, %b, LOGIC_OR) }
    | conjunction { a }
conjunction[ast_expr_t *] (left_recursive):
    | conjunction 'and' inversion >>
        { ast_binary(p, %a, %b, LOGIC_AND) }
    | inversion { a }
inversion[ast_expr_t *]:
    | 'not' inversion >>
        { ast_unary(p, %a, LOGIC_NOT) }
    | comparison { a }

comparison[ast_expr_t *]:
    | bitwise_or (comparator bitwise_or >> [ast_expr_t *]
            { ast_comp_term(p, %a, %b) })+ >>
        { ast_comparison(p, %a, %b) }
    | bitwise_or { a }
comparator[int] (inline):
    | '<' { CMP_LT }
    | '>' { CMP_GT }
    | '==' { CMP_EQ }
    | '>=' { CMP_GE }
    | '<=' { CMP_LE }
    | '!=' { CMP_NE }
    | 'in' { CMP_IN }
    | 'not' 'in' >> [int] { CMP_NI }
    | 'is' { CMP_IS }
    | 'is' 'not' >> [int] { CMP_NS }

bitwise_or[ast_expr_t *] (left_recursive):
    | bitwise_or '|' bitwise_xor >>
        { ast_binary(p, %a, %b, BINOP_IOR) }
    | bitwise_xor { a }
bitwise_xor[ast_expr_t *] (left_recursive):
    | bitwise_xor '^' bitwise_and >>
        { ast_binary(p, %a, %b, BINOP_XOR) }
    | bitwise_and { a }
bitwise_and[ast_expr_t *] (left_recursive):
    | bitwise_and '&' bitwise_shift >>
        { ast_binary(p, %a, %b, BINOP_AND) }
    | bitwise_shift { a }
bitwise_shift[ast_expr_t *] (left_recursive):
    | bitwise_shift '<<' sum >>
        { ast_binary(p, %a, %b, BINOP_SHL) }
    | bitwise_shift '>>' sum >>
        { ast_binary(p, %a, %b, BINOP_SHR) }
    | sum { a }

sum[ast_expr_t *] (left_recursive):
    | sum '+' term >>
        { ast_binary(p, %a, %b, BINOP_PLS) }
    | sum '-' term >>
        { ast_binary(p, %a, %b, BINOP_MIN) }
    | term { a }
term[ast_expr_t *] (left_recursive):
    | term '*' pipeline >>
        { ast_binary(p, %a, %b, BINOP_MUL) }
    | term '/' pipeline >>
        { ast_binary(p, %a, %b, BINOP_DIV) }
    | term '%' pipeline >>
        { ast_binary(p, %a, %b, BINOP_MOD) }
    | term '//' pipeline >>
        { ast_binary(p, %a, %b, BINOP_FDV) }
    | term '@' pipeline >>
        { ast_binary(p, %a, %b, BINOP_MML) }
    | pipeline { a }

pipeline[ast_expr_t *] (left_recursive):
    | pipeline '->' factor >>
        { ast_binary(p, %a, %b, BINOP_PIP) }
    | factor { a }

factor[ast_expr_t *] (memo):
    | '+' factor >> { ast_unary(p, %a, UNARY_PLS) }
    | '-' factor >> { ast_unary(p, %a, UNARY_MIN) }
    | '~' factor >> { ast_unary(p, %a, UNARY_INV) }
    | power { a }

```c
// Convert a primary to an expression
static ast_expr_t *ast_primary_expr(parser_t *p, void *primary) {
    ast_expr_t *r = parser_alloc(p, sizeof(ast_expr_t));
    r->opcode = BINOP_PRIMARY;
    r->left = NULL;
    r->right.primary = primary;
    return r;
}
```

power[ast_expr_t *] (memo):
    | primary '**' factor >>
        { ast_binary(p, ast_primary_expr(p, %a), %b, BINOP_POW) }
    | primary { ast_primary_expr(p, %a) }

primary[ast_primary_t *] (left_recursive):
    | primary '.' NAME >> { node(p) }
    | primary invocation >> { node(p) }
    | primary subscript >> { node(p) }
    | atom

# Parenthesized and list expressions

list_item:
    | starred_expression
    | named_expression
list_items[ast_list_t *]:
    | ','.list_item+ [','] { a }
paren_expression[ast_primary_t *] (inline):
    | '(' [list_items] ')'
list_expression[ast_primary_t *] (inline):
    | '[' [list_items] ']'

# Builder definitions
typed_parameter:
    | NAME [':' expression >> { a }]
builder_parameter:
    | typed_parameter ['=' expression >> { a }]
    | '*' typed_parameter >> { a }
    | '**' typed_parameter >> { a }
simple_parameter:
    | NAME ['=' expression >> { a }]
builder_parameters (inline):
    | '(' [','.builder_parameter+ [','] >> { a }] ')' { a }
builder_generics (inline):
    | '[' expressions ']' { a }

builder_expression[ast_primary_t *] (inline):
    | NAME [builder_generics] [builder_parameters] block_suite
    | NAME ','.simple_parameter+ ':' expression

```c
static ast_primary_t *ast_name_atom(parser_t *p, token_t *name) {
    return node(p);
}

static ast_primary_t *ast_number_atom(parser_t *p, token_t *name) {
    return node(p);
}

static ast_primary_t *ast_string_atom(parser_t *p, token_t *name) {
    return node(p);
}

static ast_primary_t *ast_none(parser_t *p) {
    return node(p);
}

static ast_primary_t *ast_true(parser_t *p) {
    return node(p);
}

static ast_primary_t *ast_false(parser_t *p) {
    return node(p);
}
```

atom[ast_primary_t *] (memo):
    | paren_expression
    | list_expression
    | builder_expression
    | NAME { ast_name_atom(p, %a) }
    | NUMBER { ast_number_atom(p, %a) }
    | STRING { ast_string_atom(p, %a) }
    | 'None' { ast_none(p) }
    | 'True' { ast_true(p) }
    | 'False' { ast_false(p) }

```c

// Provide the static parser struct
parser_t *get_static_parser() {
    return &_static_parser;
}

// Parser Entry Point
void *parse_grammar(parser_t *p, int entry_point) {
    switch (entry_point) {
    case 0:
        return single_input(p);
    case 1:
        return file_input(p);
    case 2:
        return eval_input(p);
    default:
        return 0;
    }
}
```