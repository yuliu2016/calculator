# Grammar for Fuga

single_input:
    | NEWLINE
    | ENDMARKER
    | simple_stmt
    | compound_stmt NEWLINE
file_input:
    | [stmt_list] ENDMARKER
eval_input:
    | exprlist NEWLINE* ENDMARKER

# Statements
stmt_list:
    | stmt+
stmt:
    | (simple_stmt | compound_stmt) NEWLINE
simple_stmt:
    | ';'.small_stmt+ [';']

# Small Statements
small_stmt:
    | 'pass'
    | 'break'
    | 'continue'
    | return_stmt
    | raise_stmt
    | del_stmt
    | nonlocal_stmt
    | assert_stmt
    | import_name
    | import_from
    | assignment

del_stmt:
    | 'del' targetlist
return_stmt:
    | 'return' [exprlist_star]
raise_stmt:
    | 'raise' expr ['from' expr]
nonlocal_stmt:
    | 'nonlocal' name_list
assert_stmt:
    | 'assert' expr [',' expr]

# Expression groups
name_list:
    | ','.NAME+
star_expr:
    | '*' bitwise_or
exprlist:
    | ','.expr+ [',']
target:
    | t_primary '.' NAME !t_lookahead
    | t_primary subscript !t_lookahead
    | NAME
    | '(' targetlist ')'
t_primary (left_recursive):
    | t_primary '.' NAME &t_lookahead
    | t_primary invocation &t_lookahead
    | t_primary subscript &t_lookahead
    | atom &t_lookahead
t_lookahead:
    | '.'
    | '('
    | '['
targetlist:
    | ','.target+ [',']
expr_or_star:
    | star_expr
    | expr
exprlist_star:
    | ','.expr_or_star+ [',']

subscript:
    | '[' slicelist ']'
slicelist:
    | ','.slice+ [',']
slice:
    | [expr] slice_expr [slice_expr]
    | expr
slice_expr:
    | ':' [expr]

dict_item:
    | expr ':' expr
    | '**' bitwise_or
dict_items:
    | ','.dict_item+ [',']
list_item:
    | star_expr
    | named_expr
list_items:
    | ','.list_item+ [',']
set_items:
    | exprlist_star
as_name:
    | 'as' NAME

iter_for:
    | 'for' targetlist 'in' disjunction [iter_if]
iter_if:
    | 'if' named_expr
iterator:
    | iter_for* 'for' targetlist [iter_if]

list_iterator:
    | expr_or_star iterator
dict_iterator:
    | dict_item iterator

# Assignments

assignment:
    | annassign
    | augassign
    | simple_assign
annassign:
    | target ':' expr ['=' exprlist]
augassign:
    | target augassign_op exprlist
simple_assign:
    | (targetlist '=')* exprlist_star
augassign_op[int]:
    | '+='  { "ast_integer(p, BINOP_PLS)" }
    | '-='  { "ast_integer(p, BINOP_MIN)" }
    | '*='  { "ast_integer(p, BINOP_MUL)" }
    | '/='  { "ast_integer(p, BINOP_DIV)" }
    | '%='  { "ast_integer(p, BINOP_MOD)" }
    | '@='  { "ast_integer(p, BINOP_MML)" }
    | '|='  { "ast_integer(p, BINOP_IOR)" }
    | '&='  { "ast_integer(p, BINOP_AND)" }
    | '^='  { "ast_integer(p, BINOP_XOR)" }
    | '//=' { "ast_integer(p, BINOP_FDV)" }
    | '**=' { "ast_integer(p, BINOP_POW)" }
    | '<<=' { "ast_integer(p, BINOP_SHL)" }
    | '>>=' { "ast_integer(p, BINOP_SHR)" }

# Import Statements

import_name:
    | 'import' dotted_as_names
import_from:
    | 'from' import_from_names 'import' import_from_items
import_from_names:
    | dotted_name
    | '.'+ [dotted_name]
import_from_items:
    | '*'
    | import_as_names_sp
    | import_as_names
import_as_names_sp:
    | '(' import_as_names [','] ')'
import_as_name:
    | NAME [as_name]
dotted_as_name:
    | dotted_name [as_name]
import_as_names:
    | ','.import_as_name+
dotted_as_names:
    | ','.dotted_as_name+
dotted_name:
    | '.'.NAME+

# Compound statements

compound_stmt:
    | if_stmt
    | while_stmt
    | for_stmt
    | try_stmt
    | with_stmt

if_stmt:
    | 'if' named_expr suite elif_stmt* [else_suite]
elif_stmt:
    | 'elif' named_expr suite
while_stmt:
    | 'while' named_expr suite [else_suite]
for_stmt:
    | 'for' targetlist 'in' exprlist suite [else_suite]
try_stmt:
    | 'try' suite (except_suite | finally_suite)
with_stmt:
    | 'with' ','.expr_as_name+ suite
expr_as_name:
    | expr [as_name]

# Suites

block_suite:
    | '{' NEWLINE stmt_list '}'
    | '{' [simple_stmt] '}'
suite:
    | ':' simple_stmt
    | block_suite
else_suite:
    | 'else' suite
finally_suite:
    | 'finally' suite
except_clause:
    | 'except' [expr_as_name] suite
except_suite:
    | except_clause+ [else_suite] [finally_suite]

# Function calls

invocation:
    | '(' [call_arg_list] ')'
call_arg_list:
    | ','.call_arg+  [',']
call_arg:
    | NAME ':=' expr
    | NAME '=' expr
    | '**' expr
    | '*' expr
    | expr

# Builder definitions

typed_arg_list:
    | kwargs
    | args_kwargs
    | full_arg_list
full_arg_list:
    | ','.default_arg+ [','  [kwargs | args_kwargs]]
args_kwargs:
    | '*' [typed_arg] (','  default_arg)* [','  [kwargs]]
kwargs:
    | '**' typed_arg [',']
default_arg:
    | typed_arg ['=' expr]
typed_arg:
    | NAME [':' expr]
simple_arg:
    | NAME ['=' expr]
simple_args:
    | ','.simple_arg+
builder_hint:
    | '<' name_list '>'
builder_args:
    | simple_args
    | '(' [typed_arg_list] ')'

# Expressions

named_expr[ast_named_t]:
    | NAME ':=' expr { "ast_named(%a)" }
    | expr { "ast_unnamed(%a)" }
conditional[ast_expr_t]:
    | 'if' disjunction '?' disjunction ':' expr { "ast_conditional(p, %a, %b, %c)"}
expr[ast_expr_t]:
    | conditional { a }
    | disjunction { a }
disjunction[ast_expr_t] (left_recursive):
    | disjunction 'or' conjunction { "ast_binary(p, %a, %b, LOGIC_OR)" }
    | conjunction { a }
conjunction[ast_expr_t] (left_recursive):
    | conjunction 'and' inversion { "ast_binary(p, %a, %b, LOGIC_AND)" }
    | inversion { a }
inversion[ast_expr_t]:
    | 'not' inversion { "ast_unary(p, %a, LOGIC_NOT)"}
    | comparison { a }
comparison[ast_expr_t]:
    | bitwise_or (comp_op bitwise_or { "ast_comp_term(p, %a, %b)" })+ { "ast_comparison(p, %a, %b)" }
    | bitwise_or { a }
comp_op[int]:
    | '<' { "ast_integer(p, CMP_LT)" }
    | '>' { "ast_integer(p, CMP_GT)" }
    | '==' { "ast_integer(p, CMP_EQ)" }
    | '>=' { "ast_integer(p, CMP_GE)" }
    | '<=' { "ast_integer(p, CMP_LE)" }
    | '!=' { "ast_integer(p, CMP_NE)" }
    | 'in' { "ast_integer(p, CMP_IN)" }
    | 'not' 'in' { "ast_integer(p, CMP_NI)" }
    | 'is' { "ast_integer(p, CMP_IS)" }
    | 'is' 'not' { "ast_integer(p, CMP_NS)" }
bitwise_or[ast_expr_t] (left_recursive):
    | bitwise_or '|' bitwise_xor { "ast_binary(p, %a, %b, BINOP_IOR)" }
    | bitwise_xor { a }
bitwise_xor[ast_expr_t] (left_recursive):
    | bitwise_xor '^' bitwise_and { "ast_binary(p, %a, %b, BINOP_XOR)" }
    | bitwise_and { a }
bitwise_and[ast_expr_t] (left_recursive):
    | bitwise_and '&' shift_expr { "ast_binary(p, %a, %b, BINOP_AND)" }
    | shift_expr { a }
shift_expr[ast_expr_t] (left_recursive):
    | shift_expr '<<' sum { "ast_binary(p, %a, %b, BINOP_SHL)" }
    | shift_expr '>>' sum { "ast_binary(p, %a, %b, BINOP_SHR)" }
    | sum { a }
sum[ast_expr_t] (left_recursive):
    | sum '+' term { "ast_binary(p, %a, %b, BINOP_PLS)" }
    | sum '-' term { "ast_binary(p, %a, %b, BINOP_MIN)" }
    | term { a }
term[ast_expr_t] (left_recursive):
    | term '*' pipe_expr { "ast_binary(p, %a, %b, BINOP_MUL)" }
    | term '/' pipe_expr { "ast_binary(p, %a, %b, BINOP_DIV)" }
    | term '%' pipe_expr { "ast_binary(p, %a, %b, BINOP_MOD)" }
    | term '//' pipe_expr { "ast_binary(p, %a, %b, BINOP_FDV)" }
    | term '@' pipe_expr { "ast_binary(p, %a, %b, BINOP_MML)" }
    | pipe_expr { a }
pipe_expr[ast_expr_t] (left_recursive):
    | pipe_expr '->' factor { "ast_binary(p, %a, %b, BINOP_PIP)" }
    | factor { a }
factor[ast_expr_t]:
    | '+' factor { "ast_unary(p, %a, UNARY_PLS)" }
    | '-' factor { "ast_unary(p, %a, UNARY_MIN)" }
    | '~' factor { "ast_unary(p, %a, UNARY_INV)" }
    | power { a }
power[ast_expr_t]:
    | primary '**' factor { "ast_binary(p, %a, %b, BINOP_POW)" }
    | primary { "ast_primary_expr(p, %a)" }
primary (left_recursive):
    | primary '.' NAME
    | primary invocation
    | primary subscript
    | atom
tuple_atom:
    | '(' [list_items] ')'
list_iterable:
    | '[' list_iterator ']'
list_atom:
    | '[' [list_items] ']'
set_atom:
    | '{' [set_items] '}'
dict_iterable:
    | '{' dict_iterator '}'
dict_atom:
    | '{' [dict_items] '}'
builder:
    | NAME simple_args ':' expr
    | NAME [builder_hint] [builder_args] block_suite
atom:
    | tuple_atom
    | list_iterable
    | list_atom
    | set_atom
    | dict_iterable
    | dict_atom
    | builder
    | NAME
    | NUMBER
    | STRING
    | 'None'
    | 'True'
    | 'False'
