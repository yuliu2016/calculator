# Meta Grammar

grammar:
    | [NEWLINE] element+

element:
    | directive
    | rule

directive:
    | '.' NAME '(' [arguments] ')' NEWLINE
arguments:
    | ','.argument+ [',']
argument:
    | STRING

rule:
    | NAME [return_type] [rule_args] rule_suite
return_type:
    | '[' NAME ['*'] ']'
rule_args:
    | '(' ','.rule_arg+ ')'
rule_arg:
    | NAME ['=' NAME]
rule_suite:
    | ':' NEWLINE '|' alt_list NEWLINE
alt_list:
    | sequence alternative*
alternative:
    | [NEWLINE] '|' sequence
sequence:
    | primary+ [inline_hint] [result_expr]

inline_hint:
    | '>>' [return_type]
result_expr:
    | [NEWLINE] '{' expression '}'
expr_name:
    | '.'.NAME+
expr_arg:
    | '%' NAME
    | NUMBER
    | expr_call
    | expr_name
expr_call:
    | expr_name '(' ','.expr_arg+ ')'
expression:
    | expr_call
    | NAME
    | STRING

group:
    | '(' alt_list ')'
optional:
    | '[' alt_list ']'
small_optional:
    | (NAME | STRING) '?'
delimited:
    | STRING '.' item '+'
custom_match:
    | '@' expression

primary:
    | delimited
    | '&' item
    | '!' item
    | item '*'
    | item '+'
    | item
item:
    | group
    | optional
    | custom_match
    | small_optional
    | NAME
    | STRING
